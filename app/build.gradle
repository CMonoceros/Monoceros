apply plugin: 'com.android.application'
apply from: 'multiple-channel.gradle'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "kotlin-kapt"

android {

    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    lintOptions {
        abortOnError false
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    defaultConfig {
        applicationId "com.zjmzyx.monoceros"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi', 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString()
                ]
            }
        }
    }

    signingConfigs {
        Properties properties = new Properties()
        InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream();
        properties.load(inputStream)
        debug {
            storeFile file(properties.getProperty('DEBUG_STORE_FILE'))
            storePassword properties.getProperty('DEBUG_STORE_PASSWORD')
            keyAlias properties.getProperty('DEBUG_KEY_ALIAS')
            keyPassword properties.getProperty('DEBUG_KEY_PASSWORD')
        }
        release {
            storeFile file(properties.getProperty('RELEASE_STORE_FILE'))
            storePassword properties.getProperty('RELEASE_STORE_PASSWORD')
            keyAlias properties.getProperty('RELEASE_KEY_ALIAS')
            keyPassword properties.getProperty('RELEASE_KEY_PASSWORD')
        }
    }

    buildTypes {
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            lintOptions {
                disable 'MissingTranslation'
            }
            signingConfig signingConfigs.release
        }

        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            applicationIdSuffix ".beta"
            versionNameSuffix "-beta"
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        debug {
            java.srcDirs = ['src/main/java/']
            manifest.srcFile 'src/main/AndroidManifest.xml'
            if (isComponent.toBoolean()) {
                java.srcDirs += "src/debug/java/"
            } else {
                java.srcDirs += "src/release/java/"
            }
        }

        release {
            java.srcDirs = ['src/main/java/']
            manifest.srcFile 'src/main/AndroidManifest.xml'
            if (isComponent.toBoolean()) {
                java.srcDirs += "src/debug/java/"
            } else {
                java.srcDirs += "src/release/java/"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }
}

repositories {
    flatDir {
        dirs 'aars'
    }
}

dependencies {
    if (isComponent.toBoolean()) {
        compile project(':library')
    } else {
        compile project(':movies')
    }

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    kapt "com.android.databinding:compiler:$gradle_version"

    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    provided "org.glassfish:javax.annotation:$javax_annotation_version"
}
